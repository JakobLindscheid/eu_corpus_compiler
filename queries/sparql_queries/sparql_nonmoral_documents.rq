PREFIX cdm:<http://publications.europa.eu/ontology/cdm#>
PREFIX skos:<http://www.w3.org/2004/02/skos/core#>
PREFIX dc:<http://purl.org/dc/elements/1.1/>
PREFIX xsd:<http://www.w3.org/2001/XMLSchema#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:<http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:<http://www.w3.org/2002/07/owl#>

SELECT 
DISTINCT
(GROUP_CONCAT(distinct ?work;separator="|") as ?cellarURIs)
(GROUP_CONCAT(distinct ?langIdentifier;separator="|") as ?lang)
(GROUP_CONCAT(distinct ?mtype;separator="|") as ?mtypes)
(GROUP_CONCAT(distinct ?resType;separator="|") as ?workTypes)
(GROUP_CONCAT(distinct ?subjectLabel;separator="|") as ?subjects)
(GROUP_CONCAT(distinct ?subject;separator="|") as ?subject_ids)
(GROUP_CONCAT(distinct ?mtype;separator="|") as ?file_types)
(GROUP_CONCAT(distinct ?date;separator="|") as ?dates)

WHERE 
{
	graph ?g {
		?work rdf:type ?resType .
		VALUES ?resType {
			cdm:treaty
			cdm:regulation
			cdm:regulation_implementing
			cdm:decision
			cdm:decision_implementing
			cdm:agreement_international
			cdm:act_preparatory
		}
		?work cdm:work_date_document ?date .
		FILTER ( ?date >= "decade0-01-01"^^xsd:date && ?date < "decade1-01-01"^^xsd:date) .
		?work cdm:work_is_about_concept_eurovoc ?subject . 
		graph ?ga { 
			?subject skos:prefLabel ?subjectLabel .
			?scheme skos:notation ?schemeNotation .
			?subject skos:inScheme ?scheme .
			FILTER (lang(?subjectLabel)="en") 
		}
		VALUES ?schemeNotation {
			"0406" # political framework
			"0411" # political party
			"2411" # monetary economics
			"4026" # accounting
			"4821" # maritime and inland waterway transport
			"6411" # technology and technical regulations
			"6811" # chemistry
			"6821" # mechanical engineering
		} .
	}
	graph ?ge { 
		?exp cdm:expression_belongs_to_work ?work .
		?exp cdm:expression_uses_language ?lg . 
		graph ?lgc { ?lg dc:identifier ?langIdentifier .}
		FILTER(  str( ?langIdentifier)="ENG")
	}
	graph ?gm {
		?manif cdm:manifestation_manifests_expression ?exp .
		{?manif cdm:manifestation_type ?mtype .}
		FILTER(  str( ?mtype)="html"
				|| str( ?mtype)="html_simpl"
				|| str( ?mtype)="txt"
				|| str( ?mtype)="xhtml"
				|| str( ?mtype)="xhtml_simpl"
				|| str( ?mtype)="xml"
			)
	}
}
GROUP BY ?work